//===== rAthena Script =======================================
//= Alberta Town
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Town-specific Alberta NPCs
//===== Additional Comments: =================================
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Bug fixes and syntax. [Capuche]
//============================================================

alberta,97,51,0	script	法比安	84,{
	mes "[法比安]";
	mes "先生... 當你走遍整個世界，你會聽到一些很瘋狂的事情.";
	next;
	mes "[法比安]";
	mes "曾經, 我聽說過含有魔物力量的卡片. 如果有人剛好得到卡片, 那麼他將可以使用魔物的力量.";
	next;
	mes "[法比安]";
	mes "我想這是最近流行的傳言，或者騙局, 好讓你去蒐集所有的卡片. 我是指,一張卡片真的能擁有魔物的力量?!";
	next;
	mes "[法比安]";
	mes "認真點...";
	close;
}

alberta,53,39,0	script	斯坦納	100,{
	mes "[斯坦納]";
	mes "噢...!";
	mes "歡迎來到艾爾貝塔,";
	mes "年輕的冒險者!";
	next;
	mes "[斯坦納]";
	mes "如果我分心的話，請原諒我. 我轉來轉去, 嘗試去擬定一個計畫. 你瞧, 我聽說在Geffen的商店出售的裝甲擁有魔法的抗性.";
	next;
	mes "[斯坦納]";
	mes "如果我大量去購買它們, 然後再這裡以更高的價轉賣...";
	close;
}

alberta_in,20,183,0	script	查德	1_M_03,{
	mes "[查德]";
	mes "根據流傳，傳說中的武器－永恆之槍從來不會錯過它的鎖定目標. 不知道是不是真的...";
	next;
	mes "[查德]";
	mes "人們也說過小嬰兒是由送子鳥所製作再出貨的, 女孩挖苦人都像混球一樣, 甚至是聖誕老人存在! 但只有在聖誕村.";
	next;
	mes "[查德]";
	mes "我想...";
	mes "如果這其中任何一個有可能是真的，那...";
	close;
}

alberta,131,139,2	script	喝醉的老男人	1_M_JOBGUIDER,{
	mes "[迪格爾]";
	mes "^666666*打嗝*^000000";
	mes "你-你是做什麼的";
	mes "看什麼看? 給我滾蛋!!";
	next;
	switch(select("什麼都不說.","離他遠一點.")) {
	case 1:
		mes "[迪格爾]";
		mes "哈哈哈哈 ^666666*打嗝*^000000... 你還真的有點勇氣. 我現在可能看起來沒啥用, 但我以前可是 'Going Mary.'上的水手!";
		next;
		switch(select("從沒聽過它.","真的嗎? 不要開玩笑!")) {
		case 1:
			mes "[迪格爾]";
			mes "從沒聽過它?! 所有人都知道臭名昭彰的海盜船 'Going Mary!' ^666666*打嗝~*^000000";
			next;
			mes "[迪格爾]";
			mes "啊~ 那遙遠的過去. 如果...沒有碰上那場暴風雨就好了 ^666666*打嗝*^000000";
			next;
			mes "[迪格爾]";
			mes "啊~ 船長. 我想念我們的船長，勝過任何事... 從來沒有敵人可以活在船長的劍下.";
			close;
		case 2:
			mes "[迪格爾]";
			mes "這就對了! 從來沒有人可以捉住 'Going Mary!'的全體船員 也沒有人能用劍贏過船長!";
			next;
			mes "[迪格爾]";
			mes "船長~!!! ^666666*打嗝~*^000000 他會像這樣揮舞他的劍, 這樣... 甚至這樣!!";
			next;
			mes "[迪格爾]";
			mes "和船長戰鬥的混蛋, 和任何在他身旁的朋友, 全都被火焰包圍著!";
			next;
			mes "[迪格爾]";
			mes "老兄, 那把劍一定鑲嵌了某些神秘的力量, 或者只是船長本身就很猛...!";
			next;
			mes "[迪格爾]";
			mes "呸~~ ^666666*啜泣* *啜泣...*^000000 天啊, 我好想念你們! 現在我很鬱卒～請你離開吧.";
			close;
		}
	case 2:
		mes "[迪格爾]";
		mes "這就對了!";
		mes "你走吧~";
		close;
	}
}

alberta,58,80,2	script	沙基爾	99,{
	mes "[沙基爾]";
	if (rand(2)) {
		mes "我們商人在出售商品時，擁有自己的一套談判技巧. 這個技巧可以讓我們賣商品時獲得更多的錢.";
		next;
		mes "[沙基爾]";
		mes "它不僅僅是叫喊“你必須給更多的錢！ 你需要有魅力，要懂得修飾措辭！";
		next;
		mes "[沙基爾]";
		mes "我們可以藉著這難以置信的技能多獲得24%的戒尼(Zeny). 但記住，要勤加練習才能獲得它!!";
	} else {
		mes "我們商人可以在路邊開設攤位來做生意";
		next;
		mes "[沙基爾]";
		mes "有了折扣季能, 我們可以在商店中買到真正便宜的商品，並且放在我們租用的手推車中";
		next;
		mes "[沙基爾]";
		mes "然後，我們可以在任何地方旅行，出售我們的貨物賺取利潤";
		next;
		mes "[沙基爾]";
		mes "這樣一來, 貿易更加方便、安全. 記住別睡著， 雖然不太可能做到就是了.";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "Hey, you know, this one time I was walking through the forest and I saw this little green stem moving around.";
		next;
		mes "[Sonya]";
		mes "I went to see what it was and when I went to touch it. The stem actually slapped my hand!";
		next;
		mes "[Sonya]";
		mes "It startled me, so I jumped back a bit and then I realized it wasn't a stem, but a very small animal.";
		next;
		mes "[Sonya]";
		mes "I was lucky I didn't upset it. Even the smallest animal can be dangerous if angered.";
		close;
	case 1:
		mes "You know those lazy looking bears that live in the forest on the way to Payon?";
		next;
		mes "[Sonya]";
		mes "Just for fun, I threw a rock at it and all of sudden it rushed at me! I was sooooo scared, I started to run away, then BAM!!!";
		next;
		mes "[Sonya]";
		mes "It ran into a low tree branch and knocked itself out! I swear, I'll never provoke an animal for fun again!";
		close;
	case 2:
		mes "I once saw a pack of wolves take on one of those huge, lazy bears!";
		next;
		mes "[Sonya]";
		mes "Wolves are much more cooperative than they may seem. If one of them is attacked, then any nearby wolves will run to help.";
		next;
		mes "[Sonya]";
		mes "I'd think twice if you ever want to fight one when others of its kind are around. Be careful: don't get ganged up on!";
		close;
	}
}

alberta,93,174,2	script	Grandmother Alma	103,{
	mes "[Grandmother Alma]";
	mes "Some time ago,";
	mes "a derelict ship";
	mes "drifted into";
	mes "Alberta harbour.";
	next;
	mes "[Grandmother Alma]";
	mes "Hoping to save any survivors, some of the townspeople ventured into the ship. However, they all ran out terrified, saying that corpses were walking around inside the ship.";
	next;
	mes "[Grandmother Alma]";
	mes "The ship was also packed with dangerous marine organisms, and they couldn't get inside, even if they wanted to.";
	next;
	mes "[Grandmother Alma]";
	mes "We couldn't do anything about that ominous looking ship, and just left it as it was. Nowadays, exploration teams try to enter that ship and wipe out its monsters.";
	next;
	mes "[Grandmother Alma]";
	mes "So it might be a good experience for a young person like yourself to be a recruit. But, it's still not worth risking your life if you're not strong enough.";
	close;
}

alberta,189,151,5	script	Fisk	4W_SAILOR,{
	mes "[Fisk]";
	mes "Ahoy mate,";
	mes "where'd ya";
	mes "wanna go?";
	next;
	switch(select("Sunken Ship -> 250 zeny.","Izlude Marina -> 500 zeny.","Never mind.")) {
	case 1:
		if (Zeny < 250) {
			mes "[Fisk]";
			mes "Hey now, don't try to cheat me! I said 250 zeny!";
			close;
		}
		Zeny = Zeny - 250;
		warp "alb2trea",43,53;
		end;
	case 2:
		if (Zeny < 500) {
			mes "[Fisk]";
			mes "Ain't no way yer getting there without the 500 zeny first!";
			close;
		}
		Zeny = Zeny - 500;
		if (checkre(0))
			warp "izlude",195,212;
		else
			warp "izlude",176,182;
		end;
	case 3:
		mes "[Fisk]";
		mes "Alright...";
		mes "Landlubber.";
		close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	4W_SAILOR,{
	mes "[Fisk]";
	mes "So you wanna head back to the mainland in Alberta, eh?";
	next;
	if (select("Yes please.","I changed my mind.") == 1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	4_M_04,{
	mes "[Paul]";
	mes "Good day~";
	mes "Would you like";
	mes "to join the";
	mes "exploration team";
	mes "of the Sunken Ship?";
	next;
	mes "[Paul]";
	mes "Oh! Before you join, I must warn you. If you're not that strong, you may not want to go.";
	next;
	mes "[Paul]";
	mes "So, want";
	mes "to sign up?";
	mes "The admission";
	mes "fee is only";
	mes "200 Zeny.";
	next;
	switch(select("Sign me up!","Uh, no thanks.")) {
	case 1:
		if (Zeny < 200) {
			mes "[Paul]";
			mes "It seems you don't have the money, my friend. But please come back when you're able to pay.";
			close;
		}
		Zeny = Zeny - 200;
		warp "alb2trea",62,69;
		close;
	case 2:
		mes "[Paul]";
		mes "Alright, well...";
		mes "I'll be around";
		mes "if you change";
		mes "your mind.";
		close;
	}
}

alberta,190,173,4	script	Phelix	4_M_03,{
	mes "[Phelix]";
	if ((MaxWeight - Weight) < 10000) {
		mes "Wait a moment!!";
		mes "You have brought too many things!";
		mes "You cannot accept any more items!";
		mes "Please reduce the amount of items,";
		mes "then come see me again.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "The hell are you doing here?";
		mes "There is nothing you can get for free on this ship, if you want somethin', work for it!!";
		next;
		mes "[Phelix]";
		mes "Hmm, so why don't you bring me 10 jellopies and I will give 1 potion. How's that sound?";
		mes "Or if that's too hard for your pansy ass, 3 jellopies for 1 Carrot.";
		next;
		mes "[Phelix]";
		mes "If you're interested in my offer, get me the stuff I mentioned.";
		@event_zelopy = 1;
		close;
	}
	mes "Hmm.. you want to exchange jellopies for Red Potions or some Carrots eh? Well.. which one?";
	next;
	switch(select("Red Potions please.","Carrots please.")) {
	case 1:
		mes "[Phelix]";
		mes "Alright...";
		mes "Let's see";
		mes "what'cha got...";
		next;
		mes "[Phelix]";
		if (countitem(909) < 10) {
			mes "Hey! Weren't you listening?. I said 10 jellopies for 1 Red Potion.. are ya deaf?";
			close;
		} else {
			.@max = countitem(909)/10;
			mes "Hmm, not bad...";
			mes "How many potions";
			mes "do you want to get?";
			next;
			switch(select("As many as I can, please.","I want this many.","Never mind, I like my jellopy.")) {
			case 1:
				delitem 909,.@max*10;// Jellopy
				getitem 501,.@max;// Red_Potion
				break;
			case 2:
				mes "[Phelix]";
				mes "I'm not giving you more then 100 at a time so don't bother, OK? If you don't want any, just say '0'.";
				mes "Right now, the most you can get is " + .@max + " but remember, 100 at most, you want to break my back?.";
				input .@amount;
				next;
				mes "[Phelix]";
				if (.@amount <= 0) {
					mes "Much obliged, come again anytime.";
					close;
				}
				if (.@amount > 100) {
					mes "Hey, what'd I say? 100 at a time at most, you're trying to kill me aren't you!";
					close;
				}
				if (countitem(909) < .@amount*10) {
					mes "Hmm, it looks like you don't have enough. Go get more jellopies if you want anything else from me.";
					close;
				}
				delitem 909,.@amount*10;// Jellopy
				getitem 501,.@amount;// Red_Potion
				break;
			case 3:
				mes "[Phelix]";
				mes "No problem,";
				mes "see you next time.";
				close;
			}
			mes "[Phelix]";
			mes "There you go! As I promised. Don't go suckin' them all down at once.";
			close;
		}
	case 2:
		mes "[Phelix]";
		mes "Alright, let's see what ya got...";
		next;
		mes "[Phelix]";
		if (countitem(909) < 3) {
			mes "Hmm, look pansy ass, I said 3 jellopies for 1 Carrot.. got it?";
			close;
		}
		.@max = countitem(909)/3;
		mes "Not too bad pansy...";
		mes "How many do you want?";
		next;
		switch(select("As many as I can get, please","I want this many.","Never mind, I like my jellopy.")) {
		case 1:
			delitem 909,.@max*3;// Jellopy
			getitem 515,.@max;// Carrot
			break;
		case 2:
			mes "[Phelix]";
			mes "Right I'm not giving you more then 100 at a time so don't bother, okay? If you don't want any, just say '0'.";
			input .@amount;
			next;
			mes "[Phelix]";
			if (.@amount == 0) {
				mes "Alright then, see you next time.";
				close;
			}
			if (.@amount > 100) {
				mes "Hey pansy ass, I said 100 at most, no more then that! I'm not going to break my back for the likes of you!";
				close;
			}
			if (countitem(909) < .@amount*3) {
				mes "Seems you don't have enough. Go get some more if you want anything else.";
				close;
			}
			delitem 909,.@amount*3;// Jellopy
			getitem 515,.@amount;// Carrot
			break;
		case 3:
			mes "[Phelix]";
			mes "Catch'ya later.";
			close;
		}
		mes "[Phelix]";
		mes "There you go~! As I promised. Try not to stuff yer face.";
		close;
	}
}
