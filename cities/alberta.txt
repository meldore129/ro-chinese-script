//===== rAthena Script =======================================
//= Alberta Town
//===== By: ==================================================
//= DZeroX
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Town-specific Alberta NPCs
//===== Additional Comments: =================================
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Bug fixes and syntax. [Capuche]
//============================================================

alberta,97,51,0	script	法比安	84,{
	mes "[法比安]";
	mes "先生... 當你走遍整個世界，你會聽到一些很瘋狂的事情.";
	next;
	mes "[法比安]";
	mes "曾經, 我聽說過含有魔物力量的卡片. 如果有人剛好得到卡片, 那麼他將可以使用魔物的力量.";
	next;
	mes "[法比安]";
	mes "我想這是最近流行的傳言，或者騙局, 好讓你去蒐集所有的卡片. 我是指,一張卡片真的能擁有魔物的力量?!";
	next;
	mes "[法比安]";
	mes "我是說真的...";
	close;
}

alberta,53,39,0	script	斯坦納	100,{
	mes "[斯坦納]";
	mes "噢...!";
	mes "歡迎來到艾爾貝塔,";
	mes "年輕的冒險者!";
	next;
	mes "[斯坦納]";
	mes "如果我分心的話，請原諒我. 我轉來轉去, 嘗試去擬定一個計畫. 你瞧, 我聽說在吉芬的商店出售擁有魔法抗性的鎧甲.";
	next;
	mes "[斯坦納]";
	mes "如果我大量去購買它們, 然後再這裡以更高的價轉賣...";
	close;
}

alberta_in,20,183,0	script	查德	1_M_03,{
	mes "[查德]";
	mes "根據流傳，傳說中的武器－永恆之槍從來不會錯過它的鎖定目標. 不知道是不是真的...";
	next;
	mes "[查德]";
	mes "人們也說過小嬰兒是由送子鳥所製作再出貨的, 女孩挖苦人都像混球一樣, 甚至是聖誕老人存在! 但只有在聖誕村.";//???
	next;
	mes "[查德]";
	mes "我想...";
	mes "如果這其中任何一個有可能是真的，那...";
	close;
}

alberta,131,139,2	script	喝醉的老頭	1_M_JOBGUIDER,{
	mes "[迪格爾]";
	mes "^666666*打嗝*^000000";
	mes "你-你是做什麼的";
	mes "看什麼看? 給我滾蛋!!";
	next;
	switch(select("什麼都不說.","離他遠一點.")) {
	case 1:
		mes "[迪格爾]";
		mes "哈哈哈哈 ^666666*打嗝*^000000... 你還真的有點勇氣. 我現在可能看起來沒啥用, 但我以前可是 'Going Mary.'上的水手!";
		next;
		switch(select("從沒聽過它.","真的嗎? 不要開玩笑!")) {
		case 1:
			mes "[迪格爾]";
			mes "從沒聽過它?! 所有人都知道臭名昭彰的海盜船 'Going Mary!' ^666666*打嗝~*^000000";
			next;
			mes "[迪格爾]";
			mes "啊~ 那遙遠的過去. 如果...沒有碰上那場暴風雨就好了 ^666666*打嗝*^000000";
			next;
			mes "[迪格爾]";
			mes "啊~ 船長. 我想念我們的船長，勝過任何事... 從來沒有敵人可以活在船長的劍下.";
			close;
		case 2:
			mes "[迪格爾]";
			mes "這就對了! 從來沒有人可以捉住 'Going Mary!'的全體船員 也沒有人能用劍贏過船長!";
			next;
			mes "[迪格爾]";
			mes "船長~!!! ^666666*打嗝~*^000000 他會像這樣揮舞他的劍, 這樣... 甚至這樣!!";
			next;
			mes "[迪格爾]";
			mes "和船長戰鬥的混蛋, 和任何在他身旁的朋友, 全都被火焰包圍著!";
			next;
			mes "[迪格爾]";
			mes "老兄, 那把劍一定鑲嵌了某些神秘的力量, 或者只是船長本身就很猛...!";
			next;
			mes "[迪格爾]";
			mes "呸~~ ^666666*抽泣* *抽泣...*^000000 天啊, 我好想念你們! 現在我很難過～請你離開吧.";
			close;
		}
	case 2:
		mes "[迪格爾]";
		mes "這就對了!";
		mes "你走吧~";
		close;
	}
}

alberta,58,80,2	script	沙基爾	99,{
	mes "[沙基爾]";
	if (rand(2)) {
		mes "我們商人在出售商品時，擁有自己的一套談判技巧. 這個技巧可以讓我們賣商品時獲得更多的錢.";
		next;
		mes "[沙基爾]";
		mes "它不僅僅是叫喊“你必須給更多的錢！ 你需要有魅力，要懂得修飾措辭！";
		next;
		mes "[沙基爾]";
		mes "我們可以藉著這難以置信的技能多獲得24%的戒尼(Zeny). 但記住，要勤加練習才能獲得它!!";
	} else {
		mes "我們商人可以在路邊開設攤位來做生意";
		next;
		mes "[沙基爾]";
		mes "有了折扣技能, 我們可以在商店中買到真正便宜的商品，並且放在我們租用的手推車中";
		next;
		mes "[沙基爾]";
		mes "然後，我們可以在任何地方旅行，出售我們的貨物賺取利潤";
		next;
		mes "[沙基爾]";
		mes "這樣一來, 貿易更加方便、安全. 記住別睡著， 雖然不太可能就是了.";
	}
	close;
}

alberta,62,156,2	script	索尼亞	102,{
	mes "[索尼亞]";
	switch(rand(3)){
	case 0:
		mes "嘿, 你知道嗎,有一次我在森林閒逛時，我瞧見一個綠色的莖正在移動.";
		next;
		mes "[索尼亞]";
		mes "我想去瞧看看怎麼回事，並且觸碰了它. 這莖確實地拍開了我的手!";
		next;
		mes "[索尼亞]";
		mes "嚇死我了, 所以我趕緊往後一跳，我猜那不是植物的莖，而是某種小型魔物.";
		next;
		mes "[索尼亞]";
		mes "我很慶幸我沒有惹惱他. 甚至是最小的魔物也非常危險，如果它生氣的話.";
		close;
	case 1:
		mes "你知道那群住前往翡楊路上的森林，看起來很懶惰的熊嗎?";
		next;
		mes "[索尼亞]";
		mes "只是好玩,我朝其中一隻丟了塊石頭，而牠竟朝我衝了過來! 我當時嚇～～～得要死，趕緊逃跑，然後磅－！";
		next;
		mes "[索尼亞]";
		mes "牠撞上了一個低樹干後暈倒了! 我發誓, 我再也不會因為好玩而去挑釁魔物了!";
		close;
	case 2:
		mes "我曾經看過一群狼吞下那些巨大的懶熊";
		next;
		mes "[索尼亞]";
		mes "狼群比牠們看起來還要團結. 如果牠們其中一隻被攻擊，周圍附近的狼將會跑來幫助牠.";
		next;
		mes "[索尼亞]";
		mes "我想了一下，如果你真的想與牠們戰鬥的話. 請小心: 不要讓自己被圍住!";
		close;
	}
}

alberta,93,174,2	script	老奶奶 歐瑪	103,{
	mes "[老奶奶 歐瑪]";
	mes "前些日子,";
	mes "一艘廢棄的船";
	mes "漂流至艾爾貝塔港灣";
	next;
	mes "[老奶奶 歐瑪]";
	mes "一些鎮民希望能夠拯救任何生還者，於是進入了船內，然而，他們卻全都害怕的逃了出來，說船內到處有死屍行走著";
	next;
	mes "[老奶奶 歐瑪]";
	mes "這艘船內也住有危險的海洋魔物，讓他們想進去內部也沒辦法.";
	next;
	mes "[老奶奶 歐瑪]";
	mes "過去，我們對那艘看似受詛咒的沉船無能為力，只能由它停在那；現在，探險隊正嘗試去進入那艘船並且消滅那些魔物.";
	next;
	mes "[老奶奶 歐瑪]";
	mes "所以對你這種年輕人來說，成為冒險隊徵募的新兵，會是很好的經驗，但如果你本身實力不夠，這不值得你冒著生命危險.";
	close;
}

alberta,189,151,5	script	費斯科	4W_SAILOR,{
	mes "[費斯科]";
	mes "喂～夥計,";
	mes "想去哪兒呀?";
	next;
	switch(select("沉沒之船 -> 250 戒尼.","依斯魯德碼頭 -> 500 戒尼.","沒事.")) {
	case 1:
		if (Zeny < 250) {
			mes "[費斯科]";
			mes "夥計，你耍我啊，我說250戒尼！";
			close;
		}
		Zeny = Zeny - 250;
		warp "alb2trea",43,53;
		end;
	case 2:
		if (Zeny < 500) {
			mes "[費斯科]";
			mes "沒拿到500戒尼之前，我可不能帶你過去";
			close;
		}
		Zeny = Zeny - 500;
		if (checkre(0))
			warp "izlude",195,212;
		else
			warp "izlude",176,182;
		end;
	case 3:
		mes "[費斯科]";
		mes "好吧...";
		mes "旱鴨子.";//Landlubber意指菜鳥水手、沒出過海的人，或對出航感到恐懼的人，故而翻成旱鴨子
		close;
	}
}

alb2trea,39,50,6	script	費斯科#a2t	4W_SAILOR,{
	mes "[費斯科]";
	mes "所以你想掉頭回去艾爾帕蘭本地了, 嗯?";
	next;//拜託了，幫我撐10秒!
	if (select("是的 拜託了.","我改變心意了.") == 1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	保羅	4_M_04,{
	mes "[保羅]";
	mes "美好的一天~";
	mes "你想不想加入沉沒之船的冒險隊呢？";
	next;
	mes "[保羅]";
	mes "噢! 在你加入之前，我必須警告你. 如果你實力不夠，你不會想進去裡面的.";
	next;
	mes "[保羅]";
	mes "所以，考慮得如何？";
	mes "加入只需要繳 200 戒尼的費用";
	next;
	switch(select("讓我加入!","啊, 不用了 謝謝.")) {
	case 1:
		if (Zeny < 200) {
			mes "[保羅]";
			mes "看起來你並沒有足夠的錢，等你可以付之後再回來找我吧.";
			close;
		}
		Zeny = Zeny - 200;
		warp "alb2trea",62,69;
		close;
	case 2:
		mes "[保羅]";
		mes "好吧, 嗯...";
		mes "我會一直在這";
		mes "如果你改變心意的話，再來找我吧";
		close;
	}
}

alberta,190,173,4	script	菲力克斯	4_M_03,{
	mes "[菲力克斯]";
	if ((MaxWeight - Weight) < 10000) {
		mes "等等!!";
		mes "你已經帶了太多東西在身上!";
		mes "減少身上物品的數量，再來找我吧!";
		close;
	}
	if (@event_zelopy == 0) {
		mes "沒事在這做啥?";//外國沒有三小，故不翻成三小
		mes "在船上，沒有白吃的午餐，你想要什麼，那就得努力幹活！";
		next;
		mes "[菲力克斯]";
		mes "嗯...給我10個傑勒比結晶，我就給你一瓶紅色藥水，聽起來如何?";
		mes "或者對你這娘們來說太難的話，3個傑勒比結晶換一個紅蘿蔔.";
		next;
		mes "[菲力克斯]";
		mes "如果你對我的提議有興趣，就給我提到的東西吧!.";
		@event_zelopy = 1;
		close;
	}
	mes "嗯.. 你想用傑勒比結晶煥紅色藥水還是紅蘿蔔，哪一個？";
	next;
	switch(select("紅色藥水 麻煩了.","紅蘿蔔 麻煩了.")) {
	case 1:
		mes "[菲力克斯]";
		mes "好...";
		mes "讓我瞧瞧";
		mes "你給了什麼...";
		next;
		mes "[菲力克斯]";
		if (countitem(909) < 10) {
			mes "嘿!我說10個傑勒比結晶換一瓶紅色藥水，你耳聾了?";
			close;
		} else {
			.@max = countitem(909)/10;
			mes "嗯, 不錯...";
			mes "你想換幾瓶藥水?";
			next;
			switch(select("能換多少就換多少, 麻煩了.","我想要這個數量.","還是算了, 我愛我的傑勒比結晶.")) {
			case 1:
				delitem 909,.@max*10;// Jellopy
				getitem 501,.@max;// Red_Potion
				break;
			case 2:
				mes "[菲力克斯]";
				mes "我不能一次給你超過100瓶，如果你都不想要，輸入0即可";
				mes "現在, 最多你可以換 " + .@max + " 瓶，一次最高100，除非你想累死我的背";
				input .@amount;
				next;
				mes "[菲力克斯]";
				if (.@amount <= 0) {
					mes "非常感謝, 隨時再來.";
					close;
				}
				if (.@amount > 100) {
					mes "嘿, 沒聽見我說的? 一次最多100瓶, 你想累死我是吧!";
					close;
				}
				if (countitem(909) < .@amount*10) {
					mes "嗯，看起來你的傑勒比結晶不夠，如果你想要從我這得到任何東西，那就去蒐集更多吧!";
					close;
				}
				delitem 909,.@amount*10;// Jellopy
				getitem 501,.@amount;// Red_Potion
				break;
			case 3:
				mes "[菲力克斯]";
				mes "沒問題,";
				mes "下次再來.";
				close;
			}
			mes "[菲力克斯]";
			mes "交易完成，就如我所承諾的! 不要一次都喝光了.";//粗話
			close;
		}
	case 2:
		mes "[菲力克斯]";
		mes "好吧, 讓我瞧瞧你給了什麼...";
		next;
		mes "[菲力克斯]";
		if (countitem(909) < 3) {
			mes "嗯，瞧你這娘們, 我說三個傑勒比結晶換一個紅蘿蔔，你有帶來嗎？";
			close;
		}
		.@max = countitem(909)/3;
		mes "還不賴啊...娘娘腔...";
		mes "你想要多少?";
		next;
		switch(select("能換多少就換多少，麻煩了","我想要這個數量.","還是算了, 我愛我的傑勒比結晶.")) {
		case 1:
			delitem 909,.@max*3;// Jellopy
			getitem 515,.@max;// Carrot
			break;
		case 2:
			mes "[菲力克斯]";
			mes "很好，我一次不能給你超過100個，懂嗎？如果你不想要，輸入0即可.";
			input .@amount;
			next;
			mes "[菲力克斯]";
			if (.@amount == 0) {
				mes "好吧，那麼下次再見.";
				close;
			}
			if (.@amount > 100) {
				mes "嘿 我說娘們，我說最多100個，不能再說了！我可不想累壞我的背，變得跟你一樣!";
				close;
			}
			if (countitem(909) < .@amount*3) {
				mes "看來你帶的數量不夠，回去蒐集再來吧.";
				close;
			}
			delitem 909,.@amount*3;// Jellopy
			getitem 515,.@amount;// Carrot
			break;
		case 3:
			mes "[菲力克斯]";
			mes "待會見.";
			close;
		}
		mes "[菲力克斯]";
		mes "交易完成，就如同我的承諾！. 可別吃得太撐了.";
		close;
	}
}
